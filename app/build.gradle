apply plugin: 'com.android.application'
apply plugin: 'AndResGuard'

android {
    signingConfigs {
        release {
            def rootProject = rootProject.file("../config/keystore.properties")
            if (!rootProject.exists()) return

            def prop = new Properties()
            new FileInputStream(rootProject).withCloseable { res -> prop.load(res) }

            if (!prop.isEmpty()) {
                keyAlias prop['keyAlias']
                keyPassword prop['keyPassword']
                storePassword prop['storePassword']
                storeFile file(prop['keyStore'])
            }
        }
    }
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.phantomvk.slideback.demo"
        minSdkVersion 14
        targetSdkVersion 30
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        resConfigs "en", "xhdpi"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def name = "${parent.name}_v${defaultConfig.versionName}_${variant.buildType.name}.apk"
            output.outputFileName = new File(name)
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation project(":slideback")

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

andResGuard {
    mappingFile = null
    use7zip = true
    useSign = true
    keepRoot = false
    fixedResName = "arg"
    mergeDuplicatedRes = true
    whiteList = [
            "R.drawable.icon",
            "R.string.com.crashlytics.*",
            "R.string.google_app_id",
            "R.string.gcm_defaultSenderId",
            "R.string.default_web_client_id",
            "R.string.ga_trackingId",
            "R.string.firebase_database_url",
            "R.string.google_api_key",
            "R.string.google_crash_reporting_api_key"
    ]
    compressFilePattern = [
            "*.png",
            "*.jpg",
            "*.jpeg",
            "*.gif",
    ]
    sevenzip {
        artifact = 'com.tencent.mm:SevenZip:1.2.21'
    }
}
